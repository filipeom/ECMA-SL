/* Auxiliary functions used to deal with enumerable properties of an ES5 object */
/* Used in the implementation of the for..in statement */

/* Enumerating the properties of an object includes enumerating properties of its prototype, and the prototype of
   the prototype, and so on, recursively; but a property of a prototype is not enumerated if it is "shadowed"
   because some previous object in the prototype chain has a property with the same name. The values of
   [[Enumerable]] attributes are not considered when determining if a property of a prototype object is shadowed
   by a previous object on the prototype chain. */
function getEnumerableProperties(object) {
  props := getEnumerablePropertiesRecursive(object, [], []);

  return fst props
};

function getEnumerablePropertiesRecursive(object, enumProps, checkedProps) {
  i := 0;
  list := obj_fields object;
  llist := l_len list;
  while(llist > i) {
    p_name := l_nth(list, i);
    p_value := object[p_name];
    if (typeof p_value = __$Obj) {
      if ((p_name = "Prototype") ||| (p_name = "JSProperties")) {
        props := getEnumerablePropertiesRecursive(p_value, enumProps, checkedProps);
        enumProps := fst props;
        checkedProps := t_nth(props, 1)
      } else {
        if ((p_value.Enumerable = true) &&& !(p_name in_list enumProps) &&& !(p_name in_list checkedProps)) {
          enumProps := l_add(enumProps, p_name)
        };
        checkedProps := l_add(checkedProps, p_name)
      }
    };
    i := i + 1
  };

  return (enumProps, checkedProps)
};

function getNextPropertyName(props, idx) {
  if ((l_len props) <= idx) {
    return "None"
  };

  return l_nth(props, idx)
}
